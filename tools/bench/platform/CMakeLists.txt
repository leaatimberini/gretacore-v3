cmake_minimum_required(VERSION 3.22)

project(greta_platform_bench LANGUAGES C CXX)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

add_executable(membw_cpu src/membw_cpu.cpp)
target_compile_options(membw_cpu PRIVATE -O3 -march=native)
add_executable(memlat_cpu src/memlat_cpu.cpp)
target_compile_options(memlat_cpu PRIVATE -O3 -march=native)
# ---- HIP bench (opcional) ----
option(GRETA_ENABLE_HIP "Enable HIP benchmarks (requires ROCm/HIP)" ON)

if (GRETA_ENABLE_HIP)
  set(GRETA_HIP_FOUND FALSE)
  set(GRETA_HIP_TARGET "")
  set(GRETA_HIP_LIBS "")
  set(GRETA_HIPBLAS_TARGET "")
  set(GRETA_HIPBLAS_LIB "")

  find_package(hip CONFIG QUIET)
  if (hip_FOUND)
    set(GRETA_HIP_FOUND TRUE)
    if (TARGET hip::device)
      set(GRETA_HIP_TARGET hip::device)
    endif()
  else()
    find_package(HIP QUIET)
    if (HIP_FOUND)
      set(GRETA_HIP_FOUND TRUE)
      if (TARGET HIP::hip_runtime)
        set(GRETA_HIP_TARGET HIP::hip_runtime)
      elseif (TARGET hip::device)
        set(GRETA_HIP_TARGET hip::device)
      elseif (DEFINED HIP_LIBRARIES)
        set(GRETA_HIP_LIBS ${HIP_LIBRARIES})
      endif()
    endif()
  endif()

  if (GRETA_HIP_FOUND)
    enable_language(HIP)
    if (DEFINED ENV{GRETA_HIP_ARCH})
      set(CMAKE_HIP_ARCHITECTURES "$ENV{GRETA_HIP_ARCH}")
    endif()
    add_executable(hip_noop_launch src/hip_noop_launch.cpp)
    target_compile_options(hip_noop_launch PRIVATE -O3)
    target_compile_definitions(hip_noop_launch PRIVATE GRETA_HAS_HIP=1)
    add_executable(hip_vec_add src/hip_vec_add.cpp)
    target_compile_options(hip_vec_add PRIVATE -O3)
    target_compile_definitions(hip_vec_add PRIVATE GRETA_HAS_HIP=1)

    set_source_files_properties(src/hip_noop_launch.cpp PROPERTIES LANGUAGE HIP)
    set_source_files_properties(src/hip_vec_add.cpp PROPERTIES LANGUAGE HIP)

    find_package(hipblas CONFIG QUIET)
    if (hipblas_FOUND)
      if (TARGET roc::hipblas)
        set(GRETA_HIPBLAS_TARGET roc::hipblas)
      elseif (TARGET hipblas::hipblas)
        set(GRETA_HIPBLAS_TARGET hipblas::hipblas)
      endif()
    endif()
    if (NOT GRETA_HIPBLAS_TARGET)
      find_library(GRETA_HIPBLAS_LIB hipblas
        HINTS /opt/rocm/lib /opt/rocm/lib64 /opt/rocm-7.2.0/lib /opt/rocm-7.2.0/lib64)
    endif()

    if (GRETA_HIP_TARGET)
      target_link_libraries(hip_noop_launch PRIVATE ${GRETA_HIP_TARGET})
      target_link_libraries(hip_vec_add PRIVATE ${GRETA_HIP_TARGET})
    elseif (GRETA_HIP_LIBS)
      target_link_libraries(hip_noop_launch PRIVATE ${GRETA_HIP_LIBS})
      target_link_libraries(hip_vec_add PRIVATE ${GRETA_HIP_LIBS})
      if (DEFINED HIP_INCLUDE_DIRS)
        target_include_directories(hip_noop_launch PRIVATE ${HIP_INCLUDE_DIRS})
        target_include_directories(hip_vec_add PRIVATE ${HIP_INCLUDE_DIRS})
      endif()
    else()
      message(STATUS "HIP encontrado pero sin target/librerias; hip_* no se compila.")
    endif()

    if (GRETA_HIPBLAS_TARGET OR GRETA_HIPBLAS_LIB)
      add_executable(hip_gemm src/hip_gemm.cpp)
      target_compile_options(hip_gemm PRIVATE -O3)
      target_compile_definitions(hip_gemm PRIVATE GRETA_HAS_HIP=1)
      set_source_files_properties(src/hip_gemm.cpp PROPERTIES LANGUAGE HIP)
      if (GRETA_HIP_TARGET)
        target_link_libraries(hip_gemm PRIVATE ${GRETA_HIP_TARGET})
      elseif (GRETA_HIP_LIBS)
        target_link_libraries(hip_gemm PRIVATE ${GRETA_HIP_LIBS})
      endif()
      if (GRETA_HIPBLAS_TARGET)
        target_link_libraries(hip_gemm PRIVATE ${GRETA_HIPBLAS_TARGET})
      elseif (GRETA_HIPBLAS_LIB)
        target_link_libraries(hip_gemm PRIVATE ${GRETA_HIPBLAS_LIB})
        target_include_directories(hip_gemm PRIVATE /opt/rocm/include)
      endif()
    else()
      message(STATUS "hipblas not found; hip_gemm not built.")
    endif()
  else()
    message(STATUS "HIP no encontrado. hip_* no se va a compilar.")
  endif()
endif()
